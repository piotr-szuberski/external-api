plugins {
	id 'org.springframework.boot' version '3.0.0-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.asciidoctor.convert' version '1.5.8'
	id 'java'
	id 'groovy'
	id "com.diffplug.spotless" version "6.3.0"
}

group = 'com.pet.project.external.api'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

check.dependsOn spotlessApply

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

	// spock
	implementation "org.codehaus.groovy:groovy:3.0.9"
	testImplementation platform("org.spockframework:spock-bom:2.1-groovy-3.0")
	testImplementation "org.spockframework:spock-core"

	// linter
	implementation group: 'org.ec4j.linters', name: 'editorconfig-linters', version: '0.2.1'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

spotless {
	java {
		importOrder()
		removeUnusedImports()
		googleJavaFormat().reflowLongStrings()
	}
	groovy {
		importOrder()
		excludeJava()
		greclipse()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
	}
}